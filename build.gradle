buildscript {
    ext.versions = [
        kotlin: '2.0.21',
        dokka: '1.5.31',
        // latest Apache Commons versions that don't require Java 8 (Android 7)
        commonsIO: '2.6',
        commonsText: '1.3'
    ]

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.3.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}"
    }
}

repositories {
    google()
    mavenCentral()
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'

android {
    compileSdkVersion 33

    defaultConfig {
        namespace "at.bitfire.vcard4android"
        minSdkVersion 19        // Android 4.1
        targetSdkVersion 33     // Android 12v2
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        resources {
            excludes += ['LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt']
        }
    }
    lint {
        disable 'AllowBackup', 'InvalidPackage'
    }

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildFeatures.buildConfig true
    }
}

kotlin.jvmToolchain(21)

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"

    implementation 'androidx.annotation:annotation:1.3.0'
    // noinspection GradleDependency
    implementation "commons-io:commons-io:${versions.commonsIO}"
    // noinspection GradleDependency
    implementation "org.apache.commons:commons-text:${versions.commonsText}"

    // ez-vcard to parse/generate vCards
    api('com.googlecode.ez-vcard:ez-vcard:0.11.3') {
        // hCard functionality not needed
        exclude group: 'org.jsoup'
        exclude group: 'org.freemarker'
    }

    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    testImplementation 'junit:junit:4.13.2'
}
